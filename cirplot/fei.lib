*
* fei spice3 library
*
* FEI - Faculdade de Engenharia Industrial
* S. B. Campo - SP - Brazil
* http://www.fei.edu.br
*
* Component library commonly used in FEI labs.
* Compiled by Daniel Bolgheroni <dbolgheroni@gmail.com>.
*
* Little or no modifications were made to adapt to style or model names.
*
* Models tested on spice-3f5 and some parameters ignored for
* compatibility.
*
* Last update: 20081106
*


*
* Fairchild BC548C
*
* http://www.fairchildsemi.com/models/PSPICE/Discrete/index.html
*
.model BC548C   NPN(Is=7.049f Xti=3 Eg=1.11 Vaf=54.76 Bf=543.1 Ise=78.17f
+               Ne=1.479 Ikf=24.96m Xtb=1.5 Br=1.2 Isc=27.51f Nc=1.775
+               Ikr=3.321 Rc=.9706 Cjc=4.25p Mjc=.3147 Vjc=.5697 Fc=.5
+               Cje=11.5p Mje=.6715 Vje=.5 Tr=10n Tf=410.7p Itf=1.12 Xtf=26.19
+               Vtf=10)
* unrecognized parameter
* +		Nk=.5381

*
* Fairchild 1N4007
*
* http://www.fairchildsemi.com/models/PSPICE/Discrete/index.html
*
.model  1N4007  D
+ IS = 3.872E-09
+ RS = 1.66E-02
+ N = 1.776
+ XTI = 3.0
+ EG = 1.110 
+ CJO = 1.519E-11
+ M = 0.3554
+ VJ = 0.5928
+ FC = 0.5
* unrecognized parameters
* + ISR = 1.356E-09
* + NR = 2.152
+ BV = 1000.0
+ IBV = 1.0E-03


*
* uA741 model
* 
* http://www.seas.upenn.edu/~jan/spice/spice.models.html
*
* University of Pennsylvania - Department of Electrical Engineering
* Macromodeled with Spice (?) by J.A. Connely/P. Choi.
*
* input resistance = 2Mohm
* output resistance = 75 Mohm
* open loop gain = 1e5 (100 dB)
* gain-bandwith product of 1 MHz of a bandwith of 10 Hz
*
* Ps.: This is a linear model and hence dos not model slewing of the ampop.
*
.subckt uA741-up 1 2 3
* +in (=1) -in (=2) out (=3)
rin 1 2 2meg
rout 6 3 75
e 4 0 1 2 100k
rbw 4 5 0.5meg
cbw 5 0 31.85nf
eout 6 0 5 0 1
.ends uA741-up


* uA741 operational amplifier "macromodel" subcircuit
* created using Parts release 4.01 on 07/05/89 at 09:09
* (REV N/A)
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | output
*                | | | | |
.subckt uA741    1 2 3 4 5
*
  c1   11 12 4.664e-12
  c2    6  7 20.00e-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61e6 -10e6 10e6 10e6 -10e6
  ga	6  0 11 12 137.7e-6
  gcm	0  6 10 99 2.574e-9
  iee  10  4 dc 10.16e-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0e3
  rc1   3 11 7.957e3
  rc2   3 12 7.957e3
  re1  13 10 2.740e3
  re2  14 10 2.740e3
  ree  10 99 19.69e6
  ro1   8  5 150
  ro2   7 99 150
  rp    3  4 18.11e3
  vb    9  0 dc 0
  vc	3 53 dc 2.600
  ve   54  4 dc 2.600
  vlim  7  8 dc 0
  vlp  91  0 dc 25
  vln   0 92 dc 25
.model dx D(Is=800.0e-18)
.model qx NPN(Is=800.0e-18 Bf=62.50)
.ends uA741


*//////////////////////////////////////////////////////
* (C) National Semiconductor, Inc.
* Models developed and under copyright by:
* National Semiconductor, Inc.
*//////////////////////////////////////////////////////
*
*//////////////////////////////////////////////////////
*LM324 Low Power Quad OPERATIONAL AMPLIFIER MACRO-MODEL
*//////////////////////////////////////////////////////
*
* connections:      non-inverting input
*                   |   inverting input
*                   |   |   positive power supply
*                   |   |   |   negative power supply
*                   |   |   |   |   output
*                   |   |   |   |   |
*                   |   |   |   |   |
.SUBCKT LM324/NS    1   2  99  50  28
*
*Features:
*Eliminates need for dual supplies
*Large DC voltage gain =             100dB
*High bandwidth =                     1MHz
*Low input offset voltage =            2mV
*Wide supply range =        +-1.5V to +-16V
*
*NOTE: Model is for single device only and simulated
*      supply current is 1/4 of total device current.
*      Output crossover distortion with dual supplies
*      is not modeled.
*
****************INPUT STAGE**************
*
IOS 2 1 5N
*^Input offset current
R1 1 3 500K
R2 3 2 500K
I1 99 4 100U
R3 5 50 517
R4 6 50 517
Q1 5 2 4 QX
Q2 6 7 4 QX
*Fp2=1.2 MHz
C4 5 6 128.27P
*
***********COMMON MODE EFFECT***********
*
I2 99 50 75U
*^Quiescent supply current
EOS 7 1 POLY(1) 16 49 2E-3 1
*Input offset voltage.^
R8 99 49 60K
R9 49 50 60K
*
*********OUTPUT VOLTAGE LIMITING********
V2 99 8 1.63
D1 9 8 DX
D2 10 9 DX
V3 10 50 .635
*
**************SECOND STAGE**************
*
EH 99 98 99 49 1
G1 98 9 POLY(1) 5 6 0 9.8772E-4 0 .3459
*Fp1=7.86 Hz
R5 98 9 101.2433MEG
C3 98 9 200P
*
***************POLE STAGE***************
*
*Fp=2 MHz
G3 98 15 9 49 1E-6
R12 98 15 1MEG
C5 98 15 7.9577E-14
*
*********COMMON-MODE ZERO STAGE*********
*
*Fpcm=10 KHz
G4 98 16 3 49 5.6234E-8               
L2 98 17 15.9M
R13 17 16 1K
*
**************OUTPUT STAGE**************
*
F6 50 99 POLY(1) V6 300U 1
E1 99 23 99 15 1
R16 24 23 17.5
D5 26 24 DX
V6 26 22 .63V
R17 23 25 17.5
D6 25 27 DX
V7 22 27 .63V
V5 22 21 0.27V
D4 21 15 DX
V4 20 22 0.27V
D3 15 20 DX
L3 22 28 500P
RL3 22 28 100K
*
***************MODELS USED**************
*
.MODEL DX D(IS=1E-15)
.MODEL QX PNP(BF=1.111E3)
*
.ENDS LM324/NS
